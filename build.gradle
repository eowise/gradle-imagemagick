plugins {
    id 'groovy'
    id 'maven-publish'
    id 'idea'
    id 'com.jfrog.bintray' version '1.8.5'
}

repositories {
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation platform('org.spockframework:spock-bom:1.3-groovy-2.5')
    testImplementation gradleTestKit()
    testImplementation 'org.spockframework:spock-core'
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
}

task('sourcesJar', type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/BinaryInfinityDev/gradle-imagemagick')
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('USERNAME')
                password = project.findProperty('gpr.key') ?: System.getenv('TOKEN')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId 'dev.binaryinfinity'
            artifactId 'gradle-imagemagick'
            version packageVersion
        }
    }
}

afterEvaluate {
    if (tasks.findByName('check')) {
        test {
            dependsOn publishToMavenLocal
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_API_KEY
    publications = ['gpr']
    pkg {
        repo = 'maven'
        name = 'gradle-imagemagick'
        userOrg = 'binaryinfinity'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/BinaryInfinityDev/gradle-imagemagick.git'
        version {
            name = packageVersion
            vcsTag = "v${packageVersion}"
        }
    }
}
